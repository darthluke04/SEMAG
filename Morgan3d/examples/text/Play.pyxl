Play
════════════════════════════════════════════════════════════════════════

let inventory = ["three coins", "a spoon", "a dagger", "two matches", "a flask", "the map"];
let choiceArray = ["WALK", "SEARCH", "USE", "SPEAK", "FIGHT", "HIDE"]
let choiceIndex = 0

frame
────────────────────────────────────────────────────────────────────────
set_background(rgb(0.4, 0.25, 0.3))

// Quest: Markup example. See the text.game.json setting for the "quest" constant
// to see how to add colors and fonts within text strings.
draw_sprite_corner_rect(uiSpritesheet[10][15], xy(8, 18), xy(SCREEN_SIZE.x - 18, 68))
draw_text({font: font, text: "Active Quest", pos: xy(½ SCREEN_SIZE.x, 12), x_align: "center", color: #000})
draw_text({font: font, text: quest, pos: xy(8, 18), y_align: "top", wrap_width: SCREEN_SIZE.x - 18, color: #000, shadow: #1102, markup: true})

// Description: This example uses replace() to show how to conveniently
// insert variables into a string.
let description = "You are in a flat field nestled between foothills. In the distance, a castle rises out of fog."
description += replace("\n\nIt is TIME. The sky is SKY and the temperature is TEMP°C.",
                       {TIME: format_number(60 * 60 * 10.2, "clock12"),
                        SKY:  "cloudy",
                        TEMP: 20})

// Inventory: An example using join() to convert arrays of strings into
// one long string with proper separators.
description += " You have " + join(inventory, ", ", ", and ", " and ", "nothing") + ".";

draw_sprite_corner_rect(uiSpritesheet[7][9], xy(8, 102), xy(SCREEN_SIZE.x - 18, 82))
draw_text({font: font, text: description, pos: xy(8, 100), y_align: "top", wrap_width: SCREEN_SIZE.x - 18, color: #ddd, shadow: #1025})

for c < size(choiceArray):
   const args = {font: buttonFont, text: choiceArray[c], color: #FFF, outline: #0003, shadow: #400,
                 x_align: "center", pos: xy((c + ½) * SCREEN_SIZE.x / size(choiceArray), 210)}
   if c == choiceIndex:
      // Highlight the active choice
      args.color = #ff0
      args.outline = #000
      args.text = "«" + args.text + "»"
   draw_text(args)

choiceIndex = loop(joy.xx + choiceIndex, size(choiceArray))
