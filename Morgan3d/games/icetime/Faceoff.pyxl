Faceoff
═══════════════════════════════════════════════════════════════════════════════

let startPosArray

enter
───────────────────────────────────────────────────────────────────────────────

// Slightly offset from the center line to prevent faceoff goal spamming,
// change randomply to vary advantages.
startPosArray = [xy(-9, 7 + 2 random() - 1), xy(-50, 2), xy(9, -7 + 2 random() - 1), xy(50, -2)]

// Add the puck back into the scene
puck = {pos: xy(0, 0), z: 0, vel: xy(0, 0), restitution: 90%, mass: 1, owner: ∅, isPuck: true, maxSpeed: 4}
referee.vel.x = 0
for player in playerArray: player.vel = xy(0, 0)


frame
───────────────────────────────────────────────────────────────────────────────

set_transform(xy(½ SCREEN_SIZE.x, ½ SCREEN_SIZE.y + 10), xy(1, -1))
set_clip({pos: xy(0, 20), z:-2048, size:xy(192, 90), z_size:4096})

drawScoreboard()
drawNets()

// Skate to targets
for p < 4:
   const player = playerArray[p]
   const target = startPosArray[p]
   
   if |target.x - player.pos.x| > 1:
      player.faceX = sign(target.x - player.pos.x)
      player.pos.x += ½ player.faceX
   else:
      player.faceX = -player.team.sideX
      
   if |target.y - player.pos.y| > 1: player.pos.y += ½ sign(target.y - player.pos.y)
   drawPlayer(player)

   
if |referee.pos.x| > 0: referee.pos.x -= ½ sign(referee.pos.x)
drawReferee(referee)

if mode_frames < 20:
   // wait 
else if mode_frames < 200:
   // Skate in
   referee.animation = 1
   if referee.pos.y > 7: referee.pos.y -= ½
else if mode_frames < 240:
   referee.animation = 0
else:
   referee.animation = 1
   play_sound(dropAudioClip)
   set_mode(Play) because "Drop puck"
