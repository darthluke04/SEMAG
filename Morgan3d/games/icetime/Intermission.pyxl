Intermission
═══════════════════════════════════════════════════════════════════════════════

let Δx, x, y

def moveUp(): ++y
def moveDown(): --y

def processZamboni():
   x += Δx
   // Nothing else is on screen with the zamboni, so we can draw the reflection
   // in the main draw call.
   draw_sprite({sprite: zamboniSprite, pos: project(xy(x, y)), scale: xy(Δx, 1)})

   if |x| <= 72: return
   
   if Δx > 0: add_frame_hook(moveUp, nil, 21)
   else:      add_frame_hook(moveDown, nil, 7)
   
   Δx = -Δx
   x += 4 Δx



enter
───────────────────────────────────────────────────────────────────────────────

Δx = +1
x = -72
y = -30


leave
───────────────────────────────────────────────────────────────────────────────

// Randomly rearrange the players off the bottom so that they appear
// to skate back on screen
for player in playerArray:
   player.pos = xy(20 ξ - 10, 10 ξ - 60)

   
frame
───────────────────────────────────────────────────────────────────────────────

set_transform(xy(½ SCREEN_SIZE.x, ½ SCREEN_SIZE.y + 10), xy(1, -1), 0, 1)
set_clip({corner:xy(0, 20), z:-2048, size:xy(192, 90), z_size:4096})

drawScoreboard()

if mode_frames < 120:
   // Skate off
   drawNets()
   
   // Skate to targets
   for player in playerArray:
      const target = xy(6 player.team.sideX, -80)
   
      if |target.x - player.pos.x| > 1:
         player.faceX = sign(target.x - player.pos.x)
         player.pos.x += ½ player.faceX
      
      if |target.y - player.pos.y| > 1: player.pos.y += ½ sign(target.y - player.pos.y)
      
      drawPlayer(player)
      
else if mode_frames < 966:
   processZamboni()
else:
   set_mode(Faceoff) because "Time"
