Title
═══════════════════════════════════════════════════════════════════════════════

const choiceArray = ["1P", "2P-coop", "2P-vs", "3P", "4P", "Demo", "Credits", "Quit", "Rules"]
let selection = 5

let titleSound

// Choose random teams
teamArray = []
teamArray[0] = clone(random_value(league))
teamArray[1] = createRandomDifferentTeam(teamArray[0])


enter
───────────────────────────────────────────────────────────────────────────────
reset_transform()
reset_clip()
set_background(labelSprite)
titleSound = play_sound(chantAudioClip, true, 150%)

set_post_effects({background:#0000, opacity: 50%})

const c = find(choiceArray, load_local("choice"))
if c ≠ ∅: selection = (c + 5) mod size(choiceArray)



leave
───────────────────────────────────────────────────────────────────────────────
stop_audio(titleSound)
play_sound(shootAudioClip)
reset_post_effects();


frame
───────────────────────────────────────────────────────────────────────────────

draw_sprite(titleSprite, xy(SCREEN_SIZE.x / 2, 48))

const N = size(choiceArray)
let choice

for i < size(choiceArray):
   const angle = 360 deg * (i - selection) / N + 90 deg
   const z = sin(angle)
   const a = (z + 3) / 4
   let color = gray(a)
   let border = gray(65% - 30% a)
   if i == round(selection) mod N: color = #ec0; border = #0; choice = choiceArray[i]
   draw_text({font: scoreFont, text: choiceArray[i],
             pos: xy(50% SCREEN_SIZE.x, 65% SCREEN_SIZE.y) + xy(75 cos angle, 8 z),
             color: color, outline: border, z: z, x_align: "center", y_align: "center"})


draw_text({font: tinyFont, text: "by Casual Effects", pos: xy(SCREEN_SIZE.x / 2, SCREEN_SIZE.y - 6), color: #5, x_align: "center"})

// Spin animation in the intro
if mode_frames < 72:
   selection += mid(12%, 12% - mode_frames * 0.2%, 2%)
else if joy.aa or joy.bb:
   if choice == "Quit": quit_game()
   else if choice == "Rules": set_mode(Rules)
   else if choice == "Credits": set_mode(Credits)
   else:
      if choice == "Demo": controlPadArray = [makeAI(), makeAI(), makeAI(), makeAI()]
      else if choice == "1P": controlPadArray = [gamepad_array[0], makeAI(), makeAI(), makeAI()]
      else if choice == "2P-coop": controlPadArray = [gamepad_array[0], gamepad_array[1], makeAI(), makeAI()]
      else if choice == "2P-vs": controlPadArray = [gamepad_array[0], makeAI(), gamepad_array[1], makeAI()]
      else if choice == "3P": controlPadArray = [gamepad_array[0], gamepad_array[1], gamepad_array[2], makeAI()]
      else if choice == "4P": controlPadArray = [gamepad_array[0], gamepad_array[1], gamepad_array[2], gamepad_array[3]]

      if choice == "Demo":
         createGame()
         set_mode(Faceoff) because "Demo Mode"
      else:
         save_local("choice", choice)
         set_mode(TeamSelect) because "1-4P"

selection = loop(selection - 4% joy.x, N)

