Instructions
════════════════════════════════════════════════════════════════════════

const state = []

enter
────────────────────────────────────────────────────────────────────────
play_sound(sound3)
resize(state, num_humans)
for i < num_humans: 
   state[i] = {pos: ¼ SCREEN_SIZE, ready: false}


frame
────────────────────────────────────────────────────────────────────────
set_background(#888)

let all_ready = true
for i < num_humans:
   with pos, ready in state[i]:
      if any_button_press(gamepad_array[i]): ready = true
      pos = clamp(3 gamepad_array[i].xy + pos, xy(0, 0), ½ SCREEN_SIZE)
      with transform, color in player_array[i]:
         if ready:
            draw_text({font:font, text:"READY", pos: pos + transform, color:color,
               x_align: "center", y_align:"center"})
         else:
            all_ready = false
            draw_disk(pos + transform, 10 + oscillate(¼ mode_frames, 4), color)

draw_text(large_font, "How to Play R. P. S.", xy(½ SCREEN_SIZE.x, 17), #fff, #5, ∅, "center")
draw_text({font: font, text: INSTRUCTIONS, pos: xy(10, 50), color: #fff, shadow: #5, wrap_width: SCREEN_SIZE.x - 20})
draw_text(font, "Press any button when ready", xy(½ SCREEN_SIZE.x, SCREEN_SIZE.y - 10), #fff, #5, ∅, "center")

if all_ready:
   stop_audio(title_music_sound)
   set_mode(Play) because "Button pressed"
   
